// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.google.guava:guava:22.0'
        classpath 'com.android.tools.build:gradle:3.4.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

//Ensure that all dependencies use the same version of the Android Support library
subprojects {
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && !details.requested.name.contains('multidex') ) {
                details.useVersion "28.0.0"
            }
        }
    }
}

task runLocalServer {
    dependsOn ':backend:appengineStart'
    doLast {
        println "Server started"
    }
}

task runAndroidTests {
    dependsOn ':app:connectedAndroidTest'
    doLast {
        println "Test finished"
    }
}

task stopLocalServer {
    dependsOn ':backend:appengineStop'
    doLast {
        println "Server stopped"
    }
}

task automateCustomTask {
    dependsOn 'runLocalServer'
    dependsOn 'runAndroidTests'
    dependsOn 'stopLocalServer'
    runLocalServer.finalizedBy runAndroidTests
    runAndroidTests.finalizedBy stopLocalServer
}